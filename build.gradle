buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

repositories {
    maven { url 'http://dl.bintray.com/populov/maven' }
    /* The MINT jar should be present at this repo according
       to the docs, but it gives a 404 error
    maven { url "http://mint.splunk.com/gradle/" } */
    maven { url 'https://github.com/kageiit/maven-repo/raw/master' }
    mavenCentral()
}

apply plugin: 'com.android.application'

android {
    signingConfigs {
        playStore
    }
    compileSdkVersion 19
    buildToolsVersion '19.1.0'
    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        versionCode 38
        versionName "3.7.1"
    }

    buildTypes {
        debug {
            zipAlign true
        }
        release {
            zipAlign true
            signingConfig signingConfigs.playStore
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')

    /* see above
    compile "com.splunk.mint:mint:4.0" */
    compile 'com.android.support:support-v4:19.+'
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.larswerkman:HoloColorPicker:1.4@aar'
    compile 'com.github.johnkil.android-progressfragment:sherlockprogressfragment:1.4.0@aar'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.squareup.okhttp:okhttp:1.5.4'
    compile 'org.ocpsoft.prettytime:prettytime:3.2.4.Final'
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
}

if (props.containsKey('STORE_FILE')
        && props.containsKey('STORE_PASSWORD')
        && props.containsKey('KEY_ALIAS')
        && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.playStore.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.playStore.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.playStore.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.playStore.keyPassword = props['KEY_PASSWORD']
} else {
    println 'signing.properties not found or incomplete'
    android.buildTypes.release.signingConfig = null
}